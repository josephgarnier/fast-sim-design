# Copyright 2019-present, Joseph Garnier
# All rights reserved.
#
# This source code is licensed under the license found in the
# LICENSE file in the root directory of this source tree.

# Inspired from: https://gitlab.kitware.com/cmake/community/wikis/FAQ#can-i-do-make-uninstall-with-cmake

if(NOT EXISTS "@LOCAL_BUILD_DIR@/install_manifest.txt")
	message(FATAL_ERROR "Cannot find install manifest: @LOCAL_BUILD_DIR@/install_manifest.txt")
endif()

file(READ "@LOCAL_BUILD_DIR@/install_manifest.txt" files)
string(REGEX REPLACE "\n" ";" files "${files}")
foreach(file IN ITEMS ${files})
	message(STATUS "Uninstalling \"${file}\"")
	if(EXISTS "${file}")
		execute_process(COMMAND
			"@CMAKE_COMMAND@" -E remove "${file}"
			OUTPUT_VARIABLE rm_out
			RESULT_VARIABLE rm_retval
		)
		if(NOT ${rm_retval} EQUAL 0)
			message(FATAL_ERROR "Problem when removing \"${file}\": ${rm_out}")
		endif()
	else()
		message(STATUS "File \"${file}\" does not exist.")
	endif()
	
	# Remove sub folders in /include and in /lib
	get_filename_component(path "${file}" DIRECTORY)
	file(GLOB contents RELATIVE "${path}" "${path}/*" )
	while((("${path}" MATCHES ".*@LOCAL_INSTALL_INCLUDE_DIR@.*")
		OR ("${path}" MATCHES ".*@LOCAL_INSTALL_LIBRARY_DIR@.*"))
		AND (NOT contents))
			message(STATUS "Removing empty directory \"${path}\"")
			execute_process(COMMAND
				"@CMAKE_COMMAND@" -E remove_directory "${path}"
				OUTPUT_VARIABLE rm_out
				RESULT_VARIABLE rm_retval
			)
			if(${rm_retval} EQUAL 0)
				get_filename_component(path "${path}" DIRECTORY)
				file(GLOB contents RELATIVE "${path}" "${path}/*" )
			else()
				message(FATAL_ERROR "Problem when removing \"${path}\"")
			endif()
	endwhile()
endforeach()
